# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=mailcap
pkgver=2.1.54
pkgrel=3
pkgdesc="Helper application and MIME type associations for file types"
url="https://github.com/InfrastructureServices/mailcap"
arch=(any)
license=(MIT)
makedepends=(git)
checkdepends=(python)
provides=(mime-types)
conflicts=(mime-types)
replaces=(mime-types)
backup=(
  etc/mailcap
  etc/mime.types
  etc/nginx/mime.types
)
source=("git+$url#tag=r${pkgver//./-}")
b2sums=('6273b657fa31311e0240a7367322f864853285ed86a4f77f6049eb9425d41b9553f01a2b007b83d61e0e61692a3c160cbfd107f72177f220d8385497b38489c6')

build() {
  cd mailcap
  make
}

check() {
  cd mailcap
  make check
}

package() {
  cd mailcap
  make DESTDIR="$pkgdir" install
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING
}

# vim:set sw=2 sts=-1 et:
