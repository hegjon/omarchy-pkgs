# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>

pkgname=python-cairosvg
pkgver=2.9.1
pkgrel=1
pkgdesc='SVG converter based on Cairo. It can export SVG files to PDF, PostScript and PNG files'
arch=('any')
url='https://cairosvg.org'
license=('LGPL-3.0-or-later')
depends=('python-cairocffi' 'python-cssselect2' 'python-defusedxml' 'python-pillow' 'python-tinycss2')
makedepends=('git' 'python-build' 'python-installer' 'python-pytest' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest')
replaces=('cairosvg')
source=("git+https://github.com/Kozea/CairoSVG.git#tag=$pkgver")
sha512sums=('975748432eb0ba257f34f1c5cd5d1f50e0fdcf441585e6314f5005cf6d8a6046da943002a92b7071d20ab7c7f7376d8fb91d0897a9fff054dd65e605c892d6d8')

prepare() {
  cd CairoSVG

  cp -r cairosvg test_non_regression/cairosvg_reference/
}

build() {
  cd CairoSVG

  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd CairoSVG

  pytest
}

package() {
  cd CairoSVG

  python -m installer --destdir="$pkgdir" dist/*.whl
}
