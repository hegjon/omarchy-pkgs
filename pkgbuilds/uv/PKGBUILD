# Maintainer: Orhun Parmaksız <orhun@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: MithicSpirit <rpc01234 at gmail dot com>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Leonidas Spyropoulos <artafinde@archlinux.org>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Guillaume Gauvrit <guillaume@gauvr.it>

pkgbase=uv
pkgname=("$pkgbase" "python-$pkgbase"{,-build})
pkgver=0.12.13
pkgrel=1
pkgdesc='An extremely fast Python package installer and resolver written in Rust'
arch=('x86_64')
url="https://github.com/astral-sh/uv"
license=('MIT' 'Apache-2.0')
makedepends=(bzip2
             cargo
             cmake
             git
             glibc
             jemalloc
             libgcc
             maturin
             python
             python-installer
             xz
             zstd)
source=("git+$url.git#tag=$pkgver")
sha256sums=('e2ffc5cbf702386d14dfcc9c2df170ce403aded4a78dda2723e2581bb5b4eae1')

_srcenv() {
  cd "$pkgbase"
  export CARGO_HOME="$srcdir"
  export CARGO_PROFILE_RELEASE_DEBUG=2
  export CARGO_PROFILE_RELEASE_STRIP=false
  export CARGO_PROFILE_RELEASE_LTO=thin
  export CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1
  export CARGO_PROFILE_RELEASE_OPT_LEVEL=3
  CFLAGS+=' -ffat-lto-objects'
}

prepare() {
  _srcenv
  cargo fetch --locked --target host-tuple
  mkdir -p completions
}

# Note --frozen doesn't work here because cargo fetch didn't get everything
# maturin ends up trying to use so we make do with --locked ...
build() {
  _srcenv
  local tripple="$(rustc --print host-tuple)"

  export ZSTD_SYS_USE_PKG_CONFIG=1
  export JEMALLOC_OVERRIDE=/usr/lib/libjemalloc.so
  export CARGO_FEATURE_UNPREFIXED_MALLOC_ON_SUPPORTED_PLATFORMS=1

  # Note: do not use --all-features as in enables a self-updater
  maturin build --locked --release --target "$tripple" --strip --compatibility linux
  maturin build --locked --release --target "$tripple" --strip --compatibility linux -m crates/uv-build/Cargo.toml
  local compgen="target/$tripple/release/uv --generate-shell-completion"
  $compgen bash > "completions/$pkgbase"
  $compgen elvish > "completions/$pkgbase.elv"
  $compgen fish > "completions/$pkgbase.fish"
  $compgen zsh > "completions/_$pkgbase"
}

check() {
  _srcenv
  # The upstream cargo tests are unit tests against a matrix of Python versions
  # using vendored Python installs. Even collapsing the matrix to match our
  # system Python version and patching around the path issues to use it,
  # a majority of the unit tests are irrelevant.
  local tripple="$(rustc --print host-tuple)"
  local _target="target/$tripple/release/uv"
  $_target -V | grep -F "$pkgname $pkgver"
}

_package_common() {
  install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE-*
  install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

package_uv() {
  depends=(
    bzip2
    libgcc
    glibc
    jemalloc
    zstd
  )

  cd "$pkgbase"
  _package_common
  local _target="target/$(rustc --print host-tuple)/release/uv"
  install -Dm0755 -t "$pkgdir/usr/bin/" "$_target"
  install -Dm0755 -t "$pkgdir/usr/bin/" "${_target}x"
  install -Dm 644 "completions/$pkgbase" -t "$pkgdir/usr/share/bash-completion/completions/"
  install -Dm 644 "completions/$pkgbase.elv" -t "$pkgdir/usr/share/elvish/lib/"
  install -Dm 644 "completions/$pkgbase.fish" -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -Dm 644 "completions/_$pkgbase" -t "$pkgdir/usr/share/zsh/site-functions/"
}

package_python-uv() {
  pkgdesc+=' - Python wrapper'
  depends=(python "$pkgbase=$pkgver")
  cd "$pkgbase"
  _package_common
  python -m installer -d "$pkgdir" target/wheels/uv-$pkgver-*.whl
  rm -rf "$pkgdir/usr/bin"
}

package_python-uv-build() {
  pkgdesc+=' - Python build backend'
  depends=(
    bzip2
    libgcc
    glibc
    python
    xz
  )
  groups=(python-build-backend)

  cd "$pkgbase"
  _package_common
  python -m installer -d "$pkgdir" target/wheels/uv_build-$pkgver-*.whl
}

# vim: ts=2 sw=2 et:
