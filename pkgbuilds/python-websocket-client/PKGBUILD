# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thore Bödecker <foxxx0@archlinux.org>
# Contributor: Matmas <matmas@matmas.net>
# Contributor: Thomas Gubler <thomasgubler@gmail.com>
# Contributor: Daniel YC Lin <dlin.tw at gmail>
# Contributor: Florian Bruhin <archlinux.org@the-compiler.org>

pkgname=python-websocket-client
pkgver=1.9.2
pkgrel=1
pkgdesc="WebSocket client library for Python"
arch=('any')
url="https://github.com/websocket-client/websocket-client"
license=('Apache-2.0')
depends=('python')
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
)
checkdepends=('python-pytest')
source=("https://pypi.io/packages/source/w/websocket-client/websocket_client-$pkgver.tar.gz")
sha512sums=('42da82b95f05fc8f4bbe85027d1ff1e95fc409282de4b1eb732ca871c32530f34e3fe7deeb150a1244777357e42bbc7d040c4875ad6247efacccc79b79829a66')

build() {
  cd websocket_client-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd websocket_client-$pkgver
  pytest
}

package() {
  cd websocket_client-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 ft=sh et:
