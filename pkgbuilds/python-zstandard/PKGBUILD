# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>
pkgname=('python-zstandard')
_pkgname='zstandard'
pkgver=0.25.0
pkgrel=3
pkgdesc="Python bindings to the Zstandard (zstd) compression library"
url="https://github.com/indygreg/python-zstandard"
depends=(
  'glibc'
  'python'
)
makedepends=(
    'python-build'
    'python-installer'
    'python-setuptools'
    'python-wheel'
)
checkdepends=(
    'python-hypothesis'
    'python-pytest'
)
optdepends=(
    'python-cffi'
)
license=('BSD-3-Clause')
arch=('x86_64')
source=("$pkgname-$pkgver.tar.gz::https://github.com/indygreg/python-zstandard/archive/$pkgver.tar.gz")
sha256sums=('851846ffe25681f7936ab2fd89130acf62b214a77b8c54e4319824f3510ab395')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    # pyproject.toml currently requires `setuptools<69.0.0` for CI reasons
    python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    cp build/"lib.linux-${CARCH}-cpython-"*/zstandard/backend_c.* zstandard/
    pytest
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python -m installer --destdir="${pkgdir}" dist/*.whl
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
