# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>

# Check compatibility with jedi on potential version bumps
_pkgbase=parso
pkgname=python-parso
pkgver=0.8.7
pkgrel=1
epoch=1
pkgdesc='Python parser that supports error recovery and round-trip parsing for different Python versions'
arch=(any)
url=https://github.com/davidhalter/parso
license=(MIT)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-sphinx
  python-wheel
)
checkdepends=(python-pytest)
source=("git+$url.git#tag=v$pkgver")
b2sums=('bf51ea1e39c78904972c7df11e8bbd7378c6f58fb5ab1c8af481170690a565851de878c259621d93dbe404c80673716f0cf299427c66c0e84e9f826a0cdf8ffc')

build() {
  cd $_pkgbase
  python -m build --wheel --skip-dependency-check --no-isolation
  sphinx-build -b text docs docs/_build/text
  sphinx-build -b man docs docs/_build/man
}

check() {
  cd $_pkgbase
  pytest test
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$_pkgbase-$pkgver.dist-info/licenses/LICENSE.txt \
    "$pkgdir"/usr/share/licenses/$pkgname

  cd $_pkgbase
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 CHANGELOG.rst README.rst docs/_build/text/*.txt -t "$pkgdir"/usr/share/doc/$pkgname
  install -Dm 644 docs/_build/man/parso.1 "$pkgdir"/usr/share/man/man1/$pkgname.1
}
