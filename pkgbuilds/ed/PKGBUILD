# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=ed
pkgver=1.22.6
pkgrel=1
pkgdesc='A POSIX-compliant line-oriented text editor'
arch=(x86_64)
license=(GPL-2.0-only)
url="https://www.gnu.org/software/ed/ed.html"
depends=(glibc
         sh)
options=(!emptydirs)
_archive="$pkgname-$pkgver"
source=("https://ftp.gnu.org/gnu/ed/$_archive.tar.lz"{,.sig})
sha256sums=('3f33b22135219c39c3c695f7b7171c2567d3e2a17c798c0a90607320cbb268f2'
            'SKIP')
validpgpkeys=(1D41C14B272A2219A739FA4F8FE99503132D7742  # Antonio Diaz <ant_diaz@teleline.es>
              1E5AEE0B18C0DEB45D64AA0325B62C9821501AA0) # Antonio Diaz Diaz <antonio@gnu.org>

build() {
	cd "$_archive"
	./configure \
		--prefix=/\
		--bindir=/usr/bin \
		--mandir=/usr/share/man \
		--infodir=/usr/share/info \
		"CPPFLAGS=$CPPFLAGS" \
		"CFLAGS=$CFLAGS" \
		"LDFLAGS=$LDFLAGS"
	make
}

check() {
	make -C "$_archive" check
}

package() {
	make -C "$_archive" "DESTDIR=$pkgdir" install
}
