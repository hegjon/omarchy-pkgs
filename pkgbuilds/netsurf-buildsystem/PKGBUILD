# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>

pkgname=netsurf-buildsystem
pkgver=1.10
pkgrel=3
pkgdesc='NetSurf build system files'
arch=(any)
url='https://download.netsurf-browser.org/libs/releases/'
license=(MIT)
source=("$url/buildsystem-1.10.tar.gz")
b2sums=('99843f0af4c57af2edc78a5ba1a4d0e3988fa622234235b4adc6f556e0550916187f385d2017cbeb42aa8136ee9cb5a987f6b29919b6b37b6b19ec58e62a0a5d')

build() {
  cd buildsystem-$pkgver
  sed -i 's:-Werror::' Makefile
  make PREFIX=/usr COMPONENT_TYPE=lib-shared
  make PREFIX=/usr COMPONENT_TYPE=lib-static
}

package() {
  cd buildsystem-$pkgver
  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE=lib-shared
  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE=lib-static
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
