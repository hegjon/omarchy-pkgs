# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase=xapian-core
pkgname=(xapian-core{,-docs})
pkgver=2.1.0
pkgrel=1
epoch=1
pkgdesc='Open source search engine library'
arch=('x86_64')
url="https://www.xapian.org/"
license=('GPL-2.0-or-later')
makedepends=('util-linux' 'zlib')
source=("https://oligarchy.co.uk/xapian/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('a3c650de333a0b6ba81f47df01d433a8fb86007a33e61509c7ceead822aa5104261fc539619054be8bed8721eff3cdd70a33bb06f6180a1fd7995642133d5ee9'
            'SKIP')
b2sums=('743e5ca19a0a646c51a06d2291a8339c891bbd05bb3c3061f8b30a822c72efedb8687a78b5559a9065a3b96016176be151f30695931b40592a5844417be405b0'
        'SKIP')
validpgpkeys=('08E2400FF7FE8FEDE3ACB52818147B073BAD2B07') # Olly Betts <olly@debian.org>

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"

  # workaround for test failure in libzim
  #   23/27 search_iterator  FAIL  0.47s  killed by signal 6 SIGABRT
  CXXFLAGS=${CXXFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS}

  ./configure --prefix=/usr
  make
}

check() {
  make check -C $pkgname-$pkgver
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_xapian-core() {
  depends=(
    'bash'
    'libgcc'
    'libstdc++'
    'glibc'
    'util-linux-libs' 'libuuid.so'
    'zlib' 'libz.so'
  )
  optdepends=(
    'xapian-core-docs: for documentation'
  )
  provides=('libxapian.so')
  # xapian-config requires libxapian.la
  options=('libtool')

  make DESTDIR="${pkgdir}" install -C $pkgname-$pkgver

  (
    cd "$pkgdir"
    _pick $pkgbase-docs usr/share/doc
  )

  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,HACKING,NEWS,README} -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_xapian-core-docs() {
  pkgdesc+=' - documentation'

  mv -v $pkgname/* "$pkgdir"
}
