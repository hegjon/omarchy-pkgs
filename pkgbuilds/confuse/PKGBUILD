# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Konstantin Gizdov <arch at kge dot pw>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Alexander Mieland (dma147) <dma147@linux-stats.org>
# Contributor: Thorsten Töpper <atsutane-tu@freethoughts.de>
# Contributor: Baptiste Jonglez <baptiste--aur at jonglez dot org>

pkgname=confuse
pkgver=3.4
pkgrel=1
pkgdesc='Small configuration file parser library for C'
arch=(x86_64)
url='https://github.com/libconfuse/libconfuse'
license=(ISC)
depends=(glibc)
makedepends=(git)
options=(!emptydirs)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('358f13f742fc3bf3beb20783159dd2d8d7f8f996866c8d809d23424a830b666a0c3150e9d4d2e40dbeda54fd7be16da08be85af3a17b72941a2e7431501415a4')
b2sums=('737568810f2897ffb573b600a82b5acc336b6560ce3fcb953d9b281964706afef85229a919bb9fbdcf512ec99221d7ef0b499f8e731725a5a13295550b55334f')

prepare() {
  cd "$pkgname"

  autoreconf -vfi
}

build() {
  cd "$pkgname"

  ./configure --prefix=/usr --enable-shared

  make
}

check() {
  cd "$pkgname"

  make check
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
