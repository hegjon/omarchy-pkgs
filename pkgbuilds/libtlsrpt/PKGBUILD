# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=libtlsrpt
pkgver=0.5.1
pkgrel=1
pkgdesc="A low-level C Library to implement TLSRPT into a MTA"
url="https://github.com/sys4/libtlsrpt"
arch=('x86_64')
license=('LGPL-3.0-or-later')
depends=('glibc')
makedepends=('asciidoctor')
provides=("${pkgname}.so")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz"
        "${pkgname}.pc")
sha256sums=('daa08d586ac8d200ed8dc298de955f33533315e4d946018febca8b5f27f55961'
            '2453d94ed066384097b210eb478a2c29f66fb081923cdd1d8c160fa116847903')

prepare() {
	# Upstream does not provide a pkg-config integration
	sed -e "s/VERSION/${pkgver}/" -i "${pkgname}.pc"

	cd "${pkgname}-${pkgver}"
	autoreconf -vif 
}

build() {
	cd "${pkgname}-${pkgver}"
	./configure \
		--prefix='/usr' \
		--mandir='/usr/share/man'
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -Dm 644 doc/* -t "${pkgdir}/usr/share/doc/${pkgname}/"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"

	install -Dm 644 "${srcdir}/${pkgname}.pc" "${pkgdir}/usr/lib/pkgconfig/${pkgname}.pc"
}
